#!/bin/csh -f
#
# $Id: dbl_to_sngl,v 1.11 1998-09-16 09:15:18 d3e129 Exp $
#
#
# modified to use the perl version to change back to the sed scripts
# reset useperl = 0
#
@ useperl = 1

if ($useperl) then

  if (-x /msrc/apps/bin/perl) then
     set perlexe    = /msrc/apps/bin/perl
  else if (-x /usr/local/bin/perl) then
     set perlexe    = /usr/local/bin/perl
  else if (-x /usr/local/gnu/bin/perl) then
     set perlexe    = /usr/local/gnu/bin/perl
  else if (-x /usr/gnu/bin/perl) then
     set perlexe    = /usr/gnu/bin/perl
  else
# assume perl is in your path
     set perlexe = perl
  endif
  set perlscript = {$NWCHEM_TOP}/src/config/dbl2sngl.pl

else

  set sed_script = {$NWCHEM_TOP}/src/config/dbl_to_sngl.sed
  set sed_script2 = {$NWCHEM_TOP}/src/config/dbl_to_sngl.2.sed

  set sufx = .dbl

endif

set JOB_LIMIT = 4
if (`hostname` == circus) set JOB_LIMIT = 20
if (`hostname` == mcurie) set JOB_LIMIT = 3
@ njob = 0

foreach file ($argv)

   echo converting $file to single precision
   if ($useperl) then	
      ($perlexe $perlscript $file) &	
   else
      (sed -f $sed_script  < $file > {$file}{$sufx} && mv -f {$file}{$sufx} $file;\
       sed -f $sed_script2 < $file > {$file}{$sufx} && mv -f {$file}{$sufx} $file)&
   endif

   @ njob ++
   if ($njob >= $JOB_LIMIT) then
     wait
     @ njob = 0
   endif

end

if ($njob > 0) wait
